import java.text.SimpleDateFormat

buildscript {
    apply from: "${project.rootDir}/dependencies.gradle"

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath gradlePlugins.springBoot,
                gradlePlugins.pact
    }
}

static def buildTimeStamp() {
    return new SimpleDateFormat("yyyyMMdd-HHmmss").format(new Date())
}

static def gitSha() {
    try {
        return "git rev-parse --short HEAD".execute().text.trim()
    } catch (ignored) {
        return "unknown"
    }
}

allprojects {
    defaultTasks 'clean', 'build'
}

subprojects {

    apply from: "$rootDir/dependencies.gradle"

    group = 'org.xpdojo.bank.cdc'

    repositories {
        mavenCentral()
    }

    apply plugin: 'java'
    apply plugin: 'au.com.dius.pact'

    dependencies {

        implementation libs.springBootStarterWeb,
                libs.springBootStarterWebFlux,
                libs.springDoc

        testImplementation libs.test.junitApi,
                libs.test.assertj,
                libs.test.jsonAssert,
                libs.test.mockito,
                libs.test.springBootStarterTest

        testRuntimeOnly libs.test.junitEngine
    }

    ext {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    test {
        useJUnitPlatform()
        systemProperties['contract.rootDir'] = "$buildDir/pacts"
        systemProperties['pact.writer.overwrite'] = true
    }
}
