buildscript {
    ext {
        springBootVersion = '2.3.12.RELEASE'
        pactVersion = '3.5.25'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}",
                "au.com.dius:pact-jvm-provider-gradle_2.12:${pactVersion}"
    }
}

allprojects {
    defaultTasks 'clean', 'build'
}

subprojects {

    apply from: "$rootDir/dependencies.gradle"

    group = 'org.xpdojo.bank.cdc'

    repositories {
        mavenCentral()
    }

    apply plugin: 'java'
    apply plugin: 'au.com.dius.pact'

    dependencies {

        implementation libs.springBootStarterWeb,
                libs.swagger,
                libs.swaggerUI

        testImplementation libs.test.junitApi,
                libs.test.assertj,
                libs.test.jsonAssert,
                libs.test.mockito,
                libs.test.springBootStarterTest

        testRuntimeOnly libs.test.junitEngine
    }

    ext {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    test {
        useJUnitPlatform()
        systemProperties['contract.rootDir'] = "$buildDir/pacts"
        systemProperties['pact.writer.overwrite'] = true
    }
}
